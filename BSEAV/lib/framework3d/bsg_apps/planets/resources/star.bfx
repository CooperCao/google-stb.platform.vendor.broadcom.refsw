// Broadcom BSG effect file
//
// Render the stars
//

OPTIONS
{
	SortOrder = FRONT_TO_BACK;
}

PASS 0
{
	SEMANTICS
	{
		u_mvpMatrix		= MATRIX4_MODEL_VIEW_PROJECTION;
		a_position		= VATTR_POSITION;
		a_color			= VATTR_COLOR;
		a_size			= VATTR_USER1;
	}

	STATE
	{
		EnableBlend     = true;
		EnableDepthTest = false;
		DepthMask		= false;
		BlendFunc       = SRC_ALPHA, ONE_MINUS_SRC_ALPHA;
	}

	VERTEX_SHADER
	{
		uniform		mat4	u_mvpMatrix;

		attribute	vec4	a_position;
		attribute	vec3	a_color;
		attribute	float	a_size;

		varying		vec3	v_color;

		void main()
		{
			v_color      = a_color;
			gl_PointSize = a_size;
			gl_Position  = u_mvpMatrix * a_position;
		}
	}

	FRAGMENT_SHADER
	{
		precision mediump float;

		varying vec3    v_color;

		void main()
		{
			vec2	v    = gl_PointCoord * 2.0 - 1.0;
			float	dist = dot(v,v);

			if (dist > 1.0)
				discard;

			dist = 1.0 - dist;

			vec3 black = vec3(0.0);

			gl_FragColor = vec4(mix(black, v_color, dist), dist);
		}
	}
}

