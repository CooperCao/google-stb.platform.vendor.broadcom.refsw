#!/usr/bin/expect
# -*-tcl-*-
#
#
# Utility for configuring Lantronix XPort Telnet/Serial adaptors, as
# used internally in the Aeroflex Attenuators.  This tool configures
# the XPort's idle timer and flush mode.
#
# $Copyright Broadcom Corporation$
# $Id: 5807c39502ed6720e07e935ad9ce6850b392ff8f $
#

# Defaults
array set opt {idletime "02:00" flushmode "60"}

exp_internal [expr {[info exists env(EXP_INTERNAL)]}]
log_user 1
set timeout 6


# Very basic argument handling
set host [lindex $argv 0]
set argv [lreplace $argv 0 0]

foreach k {idletime flushmode} {
    if {[set i [lsearch $argv -$k]] >= 0} {
	set j [expr {$i +1}]
	set opt($k) [lindex $argv $j]
	set argv [lreplace $argv $i $j]
    }
}
if {$argv ne ""} {
    puts stderr "Usage: $argv0 host \[-idletime xx:yy] \[-flushmode zz]"
    exit 1
}

# Have to sanity check options, or the setting will stall.
if {![regexp {^0?[0-7][0-7]} $opt(flushmode)]} {
    error "flushmode should be an octal bitmask: eg 60"
}
set m 0
if {![regexp {^(\d+):(\d+)$} $opt(idletime) - m s] &&
    ![regexp {^\d+$} $opt(idletime) s]} {
    error "idletime should be minutes:seconds or just seconds"
}
# canonicalize
set m [expr {$m + ($s / 60)}]
set s [expr {$s % 60}]
set opt(idletime) [format "%02d:%02d" $m $s]
set setidletime "$m\r$s"

# Connect
spawn -noecho -open [socket $host 9999]

# Press return to enter setup
exp_send "\r"
# Parse current settings.  If settings are all correct, press 8 to exit.
expect {
    -re {Disconn\s+Time:\s+(\d+:\d+)} {
	if {$expect_out(1,string) eq $opt(idletime)} {
	    exp_continue
	}
    }
    -re {Flush\s+Mode\s+:\s+(\d+)} {
	if {$expect_out(1,string) eq $opt(flushmode)} {
	    exp_continue
	}
    }
    -re {Your choice \? $} {
	exp_send "8\r"
	exp_continue
    }
    eof {
	exit
    }
}

# Settings need updating.  Press 1 to configure
exp_send "1\r"

# Update required fields, then press 9 to save and exit
expect {
    -re {FlushMode\s+\(\d+\)\s\? $} {
	exp_send "$opt(flushmode)\r"
	exp_continue
    }
    -re {DisConnTime\s+\(\d+:\d+\)\s\?$} {
	exp_send "$setidletime\r"
	exp_continue
    }
    -re {Your choice \? $} {
	exp_send "9\r"
	exp_continue
    }
    -re {[\?\)]\s+$} {
	exp_send "\r"
	exp_continue
    }
    eof {
	exit
    }
}
