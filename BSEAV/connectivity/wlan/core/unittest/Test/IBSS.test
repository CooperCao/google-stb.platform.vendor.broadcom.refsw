#!/bin/env utf
# -*-tcl-*-

#
# IBSS wrapper test
# $Id$
# $Copyright Broadcom Corporation$
#

package require UTF
package require UTF::Test::Join
package require UTF::Test::APChanspec
package require UTF::Test::controlchart

package provide UTF::Test::IBSS 2.0

UTF::Test IBSS {STA1 STA2 args} {
    UTF::Getopts {
	{chanspec.arg "" "Chanspec"}
	{nocache "Don't update performance cache"}
    }

    if {$(nocache)} {
	set ::UTF::ControlChart::readonly 1
    }

    set branch [$STA1 branchname]
    UTF::Try "IBSS connection" {
	if {$(chanspec) eq ""} {
	    # Find the best common channel
	    set (chanspec) [lindex [UTF::PerfChans $STA1 $STA2] 0]
	}
	APChanspec $STA1 $(chanspec) -loose -nobandlock
	APChanspec $STA2 $(chanspec) -loose -nobandlock
	Join $STA1 $STA2 -imode ibss
	set c [lindex [$STA2 wl chanspec] 0]
	if {$c ne $(chanspec)} {
	    error "Chanspec $c != $(chanspec)"
	}

	# Auto TCP window size setting based on phy rate
	set w [$STA1 tcpautowindow]

	if {![$STA1 cget -noframeburst]} {
	    $STA1 wl frameburst 1
	}
	if {![$STA2 cget -noframeburst]} {
	    $STA2 wl frameburst 1
	}
	controlchart [list $STA1 $STA2] \
	    -key [list $branch IBSS open] -i 2 -window $w
    }
    catch {$STA1 wl disassoc}
    catch {$STA2 wl disassoc}
}

