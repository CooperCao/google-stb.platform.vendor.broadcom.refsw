#!/bin/env utf
# -*-tcl-*-
#
# UTF test script for MBSS-based TravelRouter (Different UI from the
# MSSID-based version in Bears)
# $Id: 4fb15d290f052e2b780276ac01571e4a05f5bbca $
#

package require UTF
package require UTF::Test::ConnectAPSTA
package require UTF::Test::Mbss::Connectivity

package provide UTF::Test::Mbss::TravelRouter 2.0

UTF::Test Mbss::TravelRouter {DOWN STA UP UPSTA args} {
    UTF::Getopts {
	{chanspec.arg "3" "Chanspec"}
	{security.arg "aespsk2" "Upstream security"}
	{downsecurity.arg "aespsk2" "Downstream security"}
	{nocleanup "Don't clean up at the end of the test"}
    }

    set UPLAN [$UP lan]
    if {[set WAN [$UP wan]] eq ""} {
	set WAN $UPLAN
    }

    # Disassoc clients, to avoid roaming messages in logs.
    $STA wl disassoc
    $UPSTA wl disassoc

    set wlname1 [$DOWN wlname]
    set wlname2 ${wlname1}.1
    regexp {\d+} $wlname1 unit

    set GUEST [UTF::STA %AUTO% -host [$DOWN cget -host] -device $wlname2]

    UTF::Record "Connect Upstream client" {
	UTF::Test::ConnectAPSTA $UP $UPSTA \
	    -security $(security) -chanspec $(chanspec)
    }
    UTF::Record "Configure DownStream" {
	$DOWN configure -security {} \
	    -wepkey [$UP cget -wepkey] -wpakey [$UP cget -wpakey]
	UTF::Test::APChanspec $DOWN $(chanspec) -loose
	UTF::Test::APConfigureSecurity $DOWN -security $(security)
    }

    try {

	UTF::Record "Configure TravelRouter URE" {
	    # Note: URE must be off when changing router_disable, even
	    # though we're going to turn it back on.
	    $DOWN http apply ssid.asp [list \
					  router_disable 0 \
					  fw_disable 1 \
					  wan0_proto dhcp \
					  wl_unit $unit \
					  wl_radio 1 \
					  wl_ure 0]

	    UTF::Sleep 10 $DOWN "Allow router to reboot after URE changes"
	    $DOWN init
	}

	UTF::Record "Configure TravelRouter Uplink" {
	    $DOWN http apply ssid.asp [list \
					  wl_unit $unit \
					  wl_ure 1 \
					  wl_sta_retry_time 300 \
					  wl_bssid 0 \
					  wl_mode sta \
					  wl_bss_enabled 1 \
					  wl_ssid [$UP cget -ssid]]

	    UTF::Sleep 10 $DOWN "Allow router to reboot after URE changes"
	    $DOWN init
	}

	UTF::Record "Configure TravelRouter Guest" {
	    $DOWN http apply ssid.asp [list \
					  wl_unit $unit \
					  wl_bssid 1 \
					  wl_bss_enabled 1 \
					  wl_ssid DownStreamAP]

	    UTF::Sleep 5
	}

	UTF::Record "Verify Upstream Relay" {
	    $DOWN ping $UP -c 60
	    $DOWN ping $WAN
	    return
	}

	UTF::Record "Connect client" {
	    UTF::Test::ConnectAPSTA $GUEST $STA -security $(downsecurity)

	    # update all endpoint routing tables to include both routers
	    $STA add_networks $GUEST $UP
	    $UPSTA add_networks $GUEST $UP
	    $WAN add_networks $GUEST $UP
	    $UP add_networks $GUEST

	    UTF::Message INFO $STA "Verify Connectivity"
	    UTF::Test::Mbss::Connectivity [list $STA $UPSTA] {} $WAN \
		-firewall [expr {[$UP hostis Router] &&
				 [string is false \
				      [$UP nvram get fw_disable]]}]
	}
    } finally {
	if {!$(nocleanup)} {
	    catch {$UPSTA wl disassoc}
	    catch {$STA wl disassoc}
	    $GUEST destroy
	    $DOWN restore_defaults
	}
    }
    return
}

