#!/bin/env utf
# -*-tcl-*-

#
# UTF test script testing RTS threshold
#


package require UTF

package require UTF::Test::ConnectAPSTA

package provide UTF::Test::RTS 2.0

# AP: Router or Access point wireless interface
# STA: Station wireless interface
UTF::Test RTS {AP STA}  {

    set delta 100

    if {[$AP hostis BSDAP]} {
	set defrts 2346
    } else {
	set defrts 2347
    }

    # Set the different rts threshold list
    # Default should be last to reduce side effects
    set rts_val_list [list 256 500 750 1000 $defrts]

    # Associate the devices
    ConnectAPSTA $AP $STA

    # Cache IP addresses
    set APIP [$AP ipaddr]
    set STAIP [$STA ipaddr]
    # TARGET is a list of STA's to have their RTS threasholds
    # adjusted.
    # First pass, the AP
    # Second pass, the STA
    # Third pass, both the AP and the STA
    if {[$AP hostis Airport]} {
	# Airport doesn't support setting rtsthresh
	set targets [list [list $STA]]
    } else {
	set targets  [list \
			[list $AP] \
			[list $STA] \
			[list $AP $STA]]
    }
    foreach TARGET $targets {
	foreach rts_val $rts_val_list {

	    UTF::Try "$TARGET: RTS threshold: $rts_val" {
		set size [expr {$rts_val + $delta}]

		foreach T $TARGET {
		    $T wl rtsthresh $rts_val
		    UTF::Assert [$T wl rtsthresh] \
			[format {%1$d (0x%1$x)} $rts_val] $T "RTS threshold"
		}

		# ping with length greater than RTS threshold
		$STA ping $APIP -c 10 -s $size
	    }
	}
    }
}
