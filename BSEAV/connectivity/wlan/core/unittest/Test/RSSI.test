#!/bin/env utf
# -*-tcl-*-

#
# UTF test script for reporting RSSI and Noise values

# $Id$
# $Copyright Broadcom Corporation$
#

package require UTF
package require UTF::Test::ConnectAPSTA
package require UTF::ControlChart
package require UTF::MemChart

UTF::Test RSSI {AP STA args} {

    UTF::Getopts {
	{key.arg "" "Controlchart Key"}
	{loop.arg "1" "Loop"}
    }

    UTF::WrapSummary $UTF::SummaryDir "RSSI" "" "" {
	if {[catch {$STA wl bssid} ret] || $ret ne [$AP wl bssid]} {
	    ConnectAPSTA $AP $STA
	}
	for {set c 0} {$c < $(loop)} {incr c} {
	    UTF::Sleep 2

	    set pair [list $STA $AP]

	    UTF::Try "$STA: Idle RSSI" {
		set rssi [$STA wl rssi]
		if {![catch {$STA wl dump rssi} ret]} {
		    set dumprssi $ret
		} elseif {[regexp {Unsupported} $ret]} {
		    set dumprssi ""
		} else {
		    error "wl dump rssi: $ret"
		}
		UTF::MemChart MM -key [concat $(key) RSSI $pair] \
		    -norangecheck true -units "RSSI" \
		    -title "Idle RSSI"
		set chart [MM addsample $rssi]
		return [MM plotcontrolchart $chart]
	    }

	    foreach {- ant samples} \
		[regexp -inline -all {Ant(\d): \[([^\]]+)\]} $dumprssi] {
		    UTF::Try "$STA: Idle RSSI ant$ant" {
			UTF::ControlChart CC -s 16 \
			    -key [concat $(key) DumpRSSI $ant $pair] \
			    -norangecheck true -units "RSSI" \
			    -title "Idle RSSI"
			set chart [CC addsample [UTF::MeanMinMax $samples]]
			return [CC plotcontrolchart $chart]
		    }
		}

	    UTF::Try "$STA: Idle Noise" {
		set noise [$STA wl noise]
		if {![catch {$STA wl dump phynoise} ret]} {
		    set dumpnoise $ret
		} elseif {[regexp {Unsupported} $ret]} {
		    set dumpnoise ""
		} else {
		    error "wl dump phynoise: $ret"
		}


		UTF::MemChart MM -key [concat $(key) Noise $pair] \
		    -norangecheck true -units "Noise" \
		    -title "Idle Noise"
		set chart [MM addsample $noise]
		return [MM plotcontrolchart $chart]
	    }

	    foreach {- ant samples} \
		[regexp -inline -all {Ant(\d): \[([^\]]+)\]} $dumpnoise] {
		    UTF::Try "$STA: Idle Noise ant$ant" {
			UTF::ControlChart CC -s 16 \
			    -key [concat $(key) DumpNoise $ant $pair] \
			    -norangecheck true -units "Noise" \
			    -title "Idle Noise"
			set chart [CC addsample [UTF::MeanMinMax $samples]]
			return [CC plotcontrolchart $chart]
		    }
		}
	}
    }
}
