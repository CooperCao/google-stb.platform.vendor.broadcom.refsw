#!/bin/env utf
# -*-tcl-*-
#
# UTF test script for WDS recovery after restarts and reboots.  DOWN
# and UP should already be established as a WDS pair before running
# this test.
#
# $Id: 4a67176e2159922664f90515cb0f0c69cf285b8c $
#

package require UTF
package provide UTF::Test::WDS-Recovery 2.0

UTF::Test WDS-Recovery {DOWN UP} {

    if {[set DST [$UP wan]] eq ""} {
	set DST [$UP lan]
    }
    set dstip [$DST ipaddr]
    set lan [$DOWN lan]

    proc verify {SRC DST} {
	if {![catch {$SRC ping $DST -c 70}]} {
	    return
	}
	# Test has failed, now try a very long ping to see if it
	# recovers at all
	if {![catch {$SRC ping $DST -c 300}]} {
            error "WDS link reconnected late"
        } else {
	    error "WDS link failed to recover in 6 minutes"
	}
    }

    UTF::Try "Reboot $UP and Verify" {
	$UP reboot
	$UP wl channel
	verify $lan $dstip
    }

    UTF::Try "Restart $UP and Verify" {
	$UP restart
	$UP wl channel
	verify $lan $dstip
    }

    UTF::Try "Reboot $DOWN and Verify" {
	$DOWN reboot
	UTF::Sleep 10 $DOWN "Give console messages time to clear"
	$DOWN wl channel
	verify $lan $dstip
    }

    UTF::Try "Restart $DOWN and Verify" {
	# restart command may fail to complete because we're using
	# apshell at this point due to topology changes and there are
	# a lot of console messages coming out.
	catch {$DOWN restart}
	$DOWN wl channel
	verify $lan $dstip
    }

    return
}

