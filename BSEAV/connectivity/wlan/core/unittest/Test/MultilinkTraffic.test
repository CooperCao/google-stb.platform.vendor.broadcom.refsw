#!/bin/env utf
# -*-tcl-*-
#

#
# UTF test script to test multilink traffic
#
#
# $Copyright Broadcom Corporation$
#
package require UTF
package require UTF::Streams
package require UTF::wlstats

package provide UTF::Test::MultilinkTraffic 2.0

UTF::Test MultilinkTCPTraffic {args} {
    UTF::Getopts {
	{ap.arg "4708ap" "AP under test"}
	{stas.arg "4360br1 4360br2" "STAs for test"}
	{holdtime.arg "30" "time to hold traffic in seconds"}
	{pollfrequency.arg "1" "interval to poll stats"}
	{tcprate.arg "" "TCP shaped rate"}
	{tcptrace "enable tcptrace and pcap capture"}
	{graphtype.arg "svg" "optionally set to 'canvas' for mouseable plot"}
	{graphsize.arg "1024,768" "set the graph size"}
	{title.arg "" "Title used for graphs"}
	{w.arg "4M" "TCP window size"}
	{txbf.arg "-1" "Beamforming"}
	{pspretend.arg "-1" "pspretend"}
	{atf.arg "-1" "ATF setting"}
	{amsdu.arg "-1" "AMSDU setting"}
	{taf.arg "-1" "TAF setting"}
	{frameburst.arg  "-1" "Frameburst setting"}
	{directions.arg "down up" "traffic directions"}
	{nopktqstats "disable pktqstat polling"}
    }
    UTF::Try "wl status" {
	foreach dut [concat $(ap) $(stas)] {
	    $dut wl status
	}
	if {$(title) eq ""} {
	    set (title) "[$(ap) wl chanspec] [concat $(ap) $(stas)]"
	} else {
	    append (title) "\\n[$(ap) wl chanspec] [concat $(ap) $(stas)]"
	}
	return
    }
    if {$(txbf) ne "-1"} {
	lappend trytext TXBF=$(txbf)
    }
    if {$(pspretend) ne "-1"} {
	lappend trytext PS=$(pspretend)
    }
    if {$(atf) ne "-1"} {
	lappend trytext ATF=$(atf)
    }
    if {$(amsdu) ne "-1"} {
	lappend trytext AMSDU=$(amsdu)
    }
    if {$(taf) ne "-1"} {
	lappend trytext TAF=$(taf)
    }
    if {$(frameburst) ne "-1"} {
	lappend trytext FB=$(frameburst)
    }
    if {![info exists trytext]} {
	set trytext "defaults"
    }
    UTF::Try "$(ap) $trytext" {
	if {$(txbf) ne "-1"} {
	    $(ap) wl txbf $(txbf)
	}
	if {$(pspretend) ne "-1"} {
	    $(ap) wl pspretend_retry_limit $(pspretend)
	}
	if {$(atf) ne "-1"} {
	    $(ap) wl atf $(atf)
	}
	if {$(amsdu) ne "-1"} {
	    $(ap) wl amsdu_aggblock $(amsdu)
	}
	if {$(taf) ne "-1"} {
	    $(ap) wl taf_bypass $(taf)
	}
	if {$(frameburst) ne "-1"} {
	    $(ap) wl frameburst $(frameburst)
	}
    }
    foreach direction $(directions) {
	UTF::Try "Instantiate [string tolower ${direction}]stream objects" {
	    UTF::stream allstreams destroy
	    set streamlist {}
	    if {[string tolower $direction] eq "up"} {
		set apdir "-rx"
		set stadir "-tx"
	    } else {
		set stadir "-rx"
		set apdir "-tx"
	    }
	    foreach STA $(stas) {
		set tcp($STA) [UTF::stream %AUTO% $apdir $(ap) $stadir $STA -protocol tcp -name "tcp->$STA" -w $(w) -reportinterval 0.025]
		lappend streamlist $tcp($STA)
		if {$(tcprate) ne {}} {
		    $tcp($STA) configure -shaperrate $(tcprate)
		}
		if {$(tcptrace)} {
		    $tcp($STA) configure -tcptrace 1
		}
		set skey [$tcp($STA) id]
		if {!$(nopktqstats) && ![info exists pktqstats($STA)]} {
		    $STA wl msglevel +time
		    set pktqstats($STA) [UTF::wlstats::pktqstat %AUTO% -key "sta,$skey" -wlcmd "$STA wl pktq_stats" -name $STA]
		    set pktqstats(${(ap)},${STA}) [UTF::wlstats::pktqstat %AUTO% -key "ap,$skey" -wlcmd "$(ap) wl pktq_stats a:[[$STA lan] macaddr]" -name $(ap)]
		}
	    }
	    UTF::stream allstreams id
	    return
	}
	UTF::Try "Start traffic" {
	    $(ap) wl msglevel +time
	    foreach STA $(stas) {
		$tcp($STA) start
	    }
	    UTF::Sleep 2.0
	    foreach STA $(stas) {
		$tcp($STA) stats -clear
	    }
	    foreach STA $(stas) {
		$tcp($STA) linkcheck -now
	    }
	}
	UTF::Try "Hold traffic $(holdtime)s" {
	    set start [clock clicks -milliseconds]
	    while {[expr {[clock clicks -milliseconds] - $start}] <= [expr {int($(holdtime) * 1000)}]} {
		set wlcmdstart [clock clicks -milliseconds]
		foreach STAT [array names pktqstats] {
		    $pktqstats($STAT) sample
		}
		set adj [expr {([clock clicks -milliseconds] - $wlcmdstart) / 1000.0}]
		UTF::Sleep [expr {$(pollfrequency) - $adj}]
	    }
	    foreach STA $(stas) {
		$tcp($STA) samplers -disable
	    }
	    foreach STA $(stas) {
		catch {$tcp($STA) stop}
	    }
	    UTF::streamgraph graph -stat rate -title "$(title) $trytext" -outputtype $(graphtype) -graphsize $(graphsize) -streams $streamlist
	    set final "[graph plot] [graph plot -smoothing -append]"
	    catch {graph destroy}
	    if {$(tcptrace)} {
		UTF::Try "TCPTraces" {
		    foreach index [array names tcp] {
			set pcapfile [$tcp($index) tcptrace xfer]
			if {$pcapfile ne ""} {
			    UTF::Try "PCAP [file tail $pcapfile]" {
				return "html:<a href=\"$pcapfile\">tcp$index</a>"
			    }
			}
		    }
		}
	    }
	    foreach STA $(stas) {
		catch {$tcp($STA) destroy}
	    }
	    if {[string tolower $direction] ne "up"} {
		set ::__final $final
	    }
	    return $final
	}
	UTF::Try "Stat check: no rtrydrops" {
	    foreach FLOW [array names pktqstats] {
		UTF::Try "$FLOW" {
		    $pktqstats($FLOW) write
		    set pktstatqueue [$pktqstats($FLOW) get -queue]
		    set htmllink [$pktqstats($FLOW) myhtmllink -text "$pktstatqueue"]
		    if {$pktstatqueue ne "COMMON" && [$pktqstats($FLOW) interesting -show -search "rtrydrop"]} {
			error $htmllink
		    } else {
			return $htmllink
		    }
		}
	    }
	}
    }
    UTF::stream allstreams exit
}
